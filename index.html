<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8"></meta>
    <meta content="width=device-width, initial-scale=1" name="viewport"></meta>
    <link href="/ThorntonMelon.css" rel="stylesheet"></link>
  </head>
  <body>
    <header>
      <h1>Tool of Thought</h1>
      <h2>APL for the Practical Man</h2>
      <nav>
        <a href="/">Home</a>
        <a href="/posts">Posts</a>
        <a href="/projects">Projects</a>
        <a href="/donts">Don'ts</a>
        <a href="/links">Links</a>
        <a href="/about">About</a>
      </nav>
    </header>
    <main>
      <article>
        <header>
          <h1>
            <a href="/posts/excel-column-names">Excel Column Names</a>
          </h1>
          <h2>May 8, 2023</h2>
        </header>
        <p>Phase I, problem 3, of the 2020 Dyalog Programming Contest, <strong>Excel-lent Columns</strong> is stated thus:</p>
        <blockquote>
          <p>A Microsoft Excel spreadsheet numbers its rows counting up from 1. However Excel's columns are labelled alphabetically — beginning with A–Z, then AA–AZ, BA– BZ, up to ZA–ZZ, then AAA–AAZ and so on. Write a function that, given a right argument which is a character scalar or nonempty vector representing a valid character Excel column identifier between A and XFD, returns the corresponding column number</p>
          <p>Hint: The Decode function <code>X⊥Y</code>.</p>
          <p>Examples:</p>
          <pre><code>      (fn) 'A'
1
      (fn) 'APL'
1104
</code></pre>
        </blockquote>
        <p>The solution is concise:</p>
        <pre><code>      N2I←{26⊥⎕A⍳⍵}
      N2I¨'A' 'Z' 'AA' 'AZ' 'BA'
1 26 27 52 53
</code></pre>
        <p>With such a simple solution, one would think the inverse would be fairly easy. That is, given an integer, return the corresponding column name. If <code>decode</code> solves it one way, certainly <code>encode</code> should solve it the other way:</p>
        <pre><code>      {(' ',⎕A)[1+26 26⊤⍵]}¨1 26 27 52 53 
 A  A   AA  B   BA 
</code></pre>
        <p>Clearly not correct. The problem is that the column names are not plain old base-26, but rather bijective base-26, as a recent August 22, 2022 answer to an <a href="https://stackoverflow.com/questions/181596/how-to-convert-a-column-number-e-g-127-into-an-excel-column-e-g-aa">old question on Stack Overflow makes clear.</a></p>
        <p>This is not handled well by <code>encode</code>. While it may be possible to post-process the result of <code>encode</code> to make this work, I have not found a solution in this direction. From the referenced question and answer we can use recursion to compute the correct answer: </p>
        <pre><code>      I2N←{
           ⍺←⎕A
           n←≢⍺
           i←{q←¯1+⌈⍵÷n
              a←⍵-n×q
              q=0:,a
              a,⍨∇ q}⍵
           ⍺[i]
       }
       I2N¨1 26 27 52 53
 A  Z  AA  AZ  BA  
</code></pre>
        <p>Both of these functions are fairly easy to convert to work on vectors, but it does get a little messy.</p>
        <p>The function <code>I2N</code> works for any number of digits, which is nice. But this being APL for the practical programmer, we note that Excel columns don't exceed ZZZ (in fact much less) so it is easy to generate all possible Excel column names:</p>
        <pre><code>      n←⊃,/,¨∘.,\3⍴⊂,¨⎕A
      10↑n
 A  B  C  D  E  F  G  H  I  J 
      ¯10↑n
 ZZQ  ZZR  ZZS  ZZT  ZZU  ZZV  ZZW  ZZX  ZZY  ZZZ 
</code></pre>
        <p>With this in hand, and precomputed if we are worried about performance, we can trivially solve both the original problem and its inverse, with a single self-inverse function:</p>
        <pre><code>      ColumnLookup←{
          ⍝ ⍵ ←→ Column Index or Name
          ⍝ ← ←→ Column Name or Index
          ⍺←⊃,/,¨∘.,\3⍴⊂,¨⎕A
          2=≡⍵:⍺⍳⍵
          ⍺[⍵]
          }   
       ColumnLookup 1 26 27 52 53
 A  Z  AA  AZ  BA 
       ColumnLookup ColumnLookup 1 26 27 52 53
1 26 27 52 53
</code></pre>
        <p>If we precompute the column names, this will be the fastest and the simplest solution, though all the solutions are petty fast, even with an <code>each</code>. </p>
      </article>
      <article>
        <header>
          <h1>
            <a href="/posts/dom-via-json-performance">DOM via JSON Performance</a>
          </h1>
          <h2>April 12, 2023</h2>
        </header>
        <p>In the <a href="/posts/the-dom-via-json">previous post</a> we looked at building an APL DOM from a <code>⎕XML</code>-style matrix using <code>⎕JSON</code> rather than recursively creating namespaces by hand. Now let's look at some performance characteristics.</p>
        <p>First let's create a relatively large document and convert it to HTML:</p>
        <pre><code>      A←#.Abacus.Main
  
      LargeDoc←{
            v←25000 20⍴⍕¨⍳500000
            t←A.NewTable v
            c←A.Cells t
            c.class←⊂'cell'
            c.id←v
            t
       }
      
      d←LargeDoc 0
      h←A.DOM2HTML d 
      ≢h
20127817
</code></pre>
        <p>Now let's convert the HTML to a <code>⎕XML</code> matrix, and test the performance of the two techniques from the previous post for converting a <code>⎕XML</code> matrix to a ⎕JSON matrix: </p>
        <pre><code>  xm←⎕XML h
      cmpx 'XM2JM_NoLoop xm' 'XM2JM_Loop xm'
  XM2JM_NoLoop xm → 5.2E¯1 |    0% ⎕⎕⎕⎕⎕⎕⎕⎕                                
  XM2JM_Loop xm   → 2.4E0  | +372% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
</code></pre>
        <p>While it's no surprise that the non-looping algorithm is faster, it is remarkable that the looping technique is not that bad. In addition no attempt has been made to sqeeze any more performance out of the non-looping function, other than the initial act of removing the loop, so there may be more speed to be had.   </p>
        <p>Of more interest is the relative performance of the complete task of using <code>⎕JSON</code> verses recursion to build the DOM:</p>
        <pre><code>      HTML2DOMviaJSON←{
                ⎕JSON(⎕JSON⍠'M')XM2JM ⎕XML ⍵
            }
      
      
      cmpx 'HTML2DOMviaJSON h'  'A.HTML2DOM h'
  HTML2DOMviaJSON h → 9.4E0  |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                   
* A.HTML2DOM h      → 1.8E1  | +91% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
</code></pre>
        <p>Note that the results of these two functions don't match as they are namespaces not arrays. Note further that <code>⎕JSON</code> creates a tree where the child namespaces are true children of the parent, as opposed to the (this particular) recursive technique:  </p>
        <pre><code>        d1←HTML2DOMviaJSON h
        d2←A.HTML2DOM h 
        ⊃⊃⊃d1.Content.Content.Content
#.[JSON object].[JSON object].[JSON object].[JSON object]
        ⊃⊃⊃d2.Content.Content.Content
#.Abacus.Main.[Namespace]
</code></pre>
        <p>Recursion is clearly slower, but more testing should be done on reasonably sized and more realistic documents, especially docs with more depth.</p>
      </article>
      <article>
        <header>
          <h1>
            <a href="/posts/the-dom-via-json">The DOM via JSON</a>
          </h1>
          <h2>April 11, 2023</h2>
        </header>
        <p>In a <a href="/posts/a-document-object-model-in-apl">A Document Object Model in APL</a> the usefulness of  an APL DOM was discussed, and a function <code>HTML2DOM</code> demonstrated:</p>
        <pre><code>HTML2DOM←{
     ⍝ ⍵ ←→ HTML
     ⍝ ← ←→ DOM
     0=≢⍵:⍬
     {⍵⌷⍨⍳1=⍴⍵}0{
         m←⍵
         0=≢m:⍺
         b←m[;0]=0
         p←⍺{⍺ New 3↑1↓⍵}¨↓b⌿m
         m[;0]-←1
         p⊣p ∇¨1↓¨b⊂[0]m
     }⎕XML ⍵
 }    
</code></pre>
        <p>This function takes HTML and recursively works through the <code>⎕XML</code> matrix using the <code>New</code> function, building a tree of element namespaces. The <code>New</code> function is the same function used to build up a document from scratch. All of this code is in the <a href="https://github.com/th-carlisle-group/abacus">Abacus</a> project.</p>
        <p>In a recent discussion on <a href="https://chat.stackexchange.com/rooms/52405/the-apl-orchard">The APL Orchard</a>, it was noted that <code>⎕JSON</code> could be used to create a namespace tree from HTML. While it is no secret that <code>⎕JSON</code> creates a namespace tree from JSON (that's what it does!), it never occurred to me to use it for creating an HTML DOM. It seems it should be faster than doing it by "hand", but there is some additional overhead in going from HTML to JSON and then to a namespace tree. Let's see how to do it.</p>
        <p>The problem boils down to converting a <code>⎕XML</code>-style matrix to a <code>⎕JSON</code>-style matrix. We know what  a <code>⎕XML</code> matrix looks like, and we know how the DOM is stuctured (which is good, as half the battle is simply knowing the destination), but what on Earth does the corresponding <code>⎕JSON</code> matrix look like? Fortunately, we can create a simple document fragment:   </p>
        <pre><code>      A←#.Abacus.Main
SimpleDoc←{
     d←A.New'section'
     d.class←'post'
     d.id←'p20230411'
     h←d A.New'h1' 'Simple Doc'
     l←d A.New'ul'
     l.Content←{A.New'li'⍵}¨'One' 'Two' 'Three'
     _←(A.Elements d).⎕EX⊂'Parent'⍝ Not good for JSON
     d
 } 
      d←SampleDoc 0
      h←A.DOM2HTML d
      h
&lt;section class="post" id="p20230411"&gt;
  &lt;h1&gt;Simple Doc&lt;/h1&gt;                
  &lt;ul&gt;                               
    &lt;li&gt;One&lt;/li&gt;                     
    &lt;li&gt;Two&lt;/li&gt;                     
    &lt;li&gt;Three&lt;/li&gt;                   
  &lt;/ul&gt;                              
&lt;/section&gt;   
</code></pre>
        <p>And see what the familiar source <code>⎕XML</code> matrix looks like:</p>
        <pre><code>      ⎕XML h
0  section               class  post        3
                         id     p20230411    
1  h1       Simple Doc                      5
1  ul                                       3
2  li       One                             5
2  li       Two                             5
2  li       Three                           5
</code></pre>
        <p>And the corresponding unfamiliar target <code>⎕JSON</code> matrix:</p>
        <pre><code>      ⎕JSON⍠'M'⊢⎕JSON d
0                       1
1  Content              2
2                       1
3  Content              2
4           Simple Doc  4
3  Tag      h1          4
2                       1
3  Content              2
4                       1
5  Content              2
6           One         4
5  Tag      li          4
4                       1
5  Content              2
6           Two         4
5  Tag      li          4
4                       1
5  Content              2
6           Three       4
5  Tag      li          4
3  Tag      ul          4
1  Tag      section     4
1  class    post        4
1  id       p20230411   4
</code></pre>
        <p>The are indeed a little different. By inspection, and some trial and error, we can write a function to go directly from the <code>⎕XML</code> matrix to the <code>⎕JSON</code> matrix:</p>
        <pre><code>XM2JM←{
     ⍝ ⍵ ←→ ⎕XML matrix
     ⍝ ← ←→ ⎕JSON matrix
     ⊃⍪/{
         d e t a←4↑⍵
         k←2×d
         j←k+1
         e≡'':⍉⍪k''t 4         ⍝ Data        1 row OR...
         z←⊂k'' '' 1           ⍝ Object      (1 row)+
         z,←⊂j'Tag'e 4         ⍝ Element     (1 row)+
         z,←4,⍨¨j,¨↓a          ⍝ Attributes  (0 or more rows)+
         z,←⊂j'Content' '' 2   ⍝ Content     (1 row)+
         z,←(0&lt;≢t)/⊂(j+1)''t 4 ⍝ Text        (0 or 1 row)
         ↑z}¨↓⍵
 }
</code></pre>
        <p>This loops through each row of the <code>⎕XML</code> matrix, which is less than desirable (but surprisingly not particularly slow). However it provides us insight into how to take a more array oriented approach:</p>
        <pre><code>XM2JM←{
     ⍝ ⍵ ←→ ⎕XML matrix
     ⍝ ← ←→ ⎕JSON matrix
     (o t)←↓⍉~⍵[;1 2]∊⊂''                     ⍝ Object, text
     l←≢¨⍵[;3]                                ⍝ Number of attributes
     n←o*⍨3+t+l                               ⍝ Target rows per source row
     s←0,+\¯1↓n                               ⍝ Starting index of source in target
     j←(o∧t)/3+s+l                            ⍝ Text index
     m←0 '' '' 1⍴⍨4,⍨+/n                      ⍝ Initialize result
     m[o/1+s;1 2 3]←(⊂'Tag'),4,⍨⍪o/⍵[;1]      ⍝ Tag row
     m[2+∊s+⍳¨l;1 2 3]←4,⍨⊃⍪/⍵[;3]            ⍝ Possible attribute rows
     m[o/2+s+l;1 2 3]←(+/o)⌿⍉⍪'Content' '' 2  ⍝ Content row
     m[j;2 3]←4,⍨⍪⍵[;2]/⍨o∧t                  ⍝ Possible Text row
     m[s/⍨~o;2 3]←4,⍨⍪⍵[;2]/⍨~o               ⍝ Text row
     m[;0]←(n/2×⍵[;0])+(~m[;1]∊⊂'')+2×j∊⍨⍳≢m  ⍝ Depth
     m
 }
</code></pre>
        <p>With the exception of the depth column, the target matrix is filled in over about 6 steps. There may be a way to fill in each column in one go, as the depth is done on the penultimate line.    </p>
        <p>In the next post we will investigate the relative performance of these various techniques.</p>
      </article>
      <h2>
        <a href="/posts">More posts...</a>
      </h2>
    </main>
    <footer>
      <p>Copyright 2023 Paul S. Mansour</p>
    </footer>
  </body>
</html>
