<!DOCTYPE html>
<html lang="en"><head><meta charset="utf-8"></meta><meta content="width=device-width, initial-scale=1" name="viewport"></meta><link href="/ThorntonMelon.css" rel="stylesheet"></link></head><body><header><h1>Tool of Thought</h1><h2>APL for the Practical Man</h2><h3>"You don't know how bad your design is until you write about it."</h3><nav><a href="/">Home</a><a href="/posts">Posts</a><a href="/projects">Projects</a><a href="/donts">Don'ts</a><a href="/links">Links</a><a href="/about">About</a></nav></header><main><article><header><h1><a href="/posts/threading-the-htmlrenderer">Threading the HTMLRenderer</a></h1><h2>September 22, 2022</h2></header><p>The HTMLRenderer supports web sockets, which gives us two-way asynchronous communication between the browser and the APL session. The browser can send a message to APL, and APL can send a message to the browser. However, in neither case is the sender waiting for a reply from the recipient. Much functionality can be achieved this way.    However, there are at least two cases when, from APL, we want to send a message to the  HTMLRenderer and wait for a response. The first is simply to execute a little general JavaScript. We might want to execute '2+2' or get the innerHTML from an element, or get the HTML of the entire page.  This should work anytime, anywhere, and specifically it must work   under the event handler of the WebSocketReceive event, which is exactly where it gets tricky. </p><p>The second is when, from APL, we want to fire an event in the HTMLRenderer which has been wired with a event handler that calls back into APL. APL handles the message, presumably changing some state, and perhaps sending a message back to the HTMLRenderer, changing some state there. In this case we want to be able to wait in APL for all this to happen, and then verify the state changes. In other words, we want to test our HTMLRenderer GUI code in a easy, linear fashion, in one APL process.    This post attempts to explain an attempt to implement this synchronous behavior for these two cases. </p><p>The <code>ExecutJavaScriptSync</code> function executes a snippet of JavasScript and waits for a response:  </p><pre><code>ExecuteJavaScriptSync←{
     ⍝ ⍺ ←→ Document
     ⍝ ⍵ ←→ Javascript
     ⍝ ← ←→ Result                                                     
     c←'"',⍵,'"'
     j←'execCode(',(⍕⎕TID),',',c,')'
     _←⍺ ExecuteJavaScript j
     ⎕TGET ⎕TID
 }
</code></pre><p>The left argument is the APLDOM object, the right argument is a string of JavaScript. The thread ID is used as a message identifier.</p><p>There is also a cover function that executes a string of javascript inside a specfic element:</p><pre><code>ExecuteOnElementSync←{
     ⍝ ⍺ ←→ Element
     ⍝ ⍵ ←→ Javascript
     ⍝ ← ←→ Result
     i←TagIndex ⍺
     t←⍺.Tag
     c←'document.getElementsByTagName'
     c,←'(''',t,''').item(',(⍕i),').',⍵,';'
     ⍺.Document ExecuteJavaScriptSync c
 }
</code></pre><p>The <code>ExecuteJavaScript</code> function just sends the data over the socket:</p><pre><code>ExecuteJavaScript←{
     ⍝ ⍺ ←→ Document
     ⍝ ⍵ ←→ JavaScript
     ⍺.HTMLRenderer.WebSocketSend ⍺.HTMLRenderer.WebSocketID ⍵ 1 1
 }
</code></pre><p>Note that we do not make use of the HTMLRenderer.ExecuteJavaScript method.</p><p>Once the message is sent, we wait for a token (<code>⎕TGET ⎕TID</code>). Meanwhile, in the HTMLRenderer, the following JavaScript is executed on receipt of the message from APL:</p><pre><code>  function execCode(id,code) { 
         const b = {Event: "SJSR", id: id, result: eval(code) }; 
         const m = JSON.stringify(b); 
         ws.send (m)
}      
</code></pre><p>This simply evaluates the code and sends the result and the id back to APL. It also invents and returns an event named "SJSR" for <strong>synchronous javascript result</strong>.  </p><p>Back in APL, we are waiting for and processing messages from the HTMLRenderer via the HTMLRenderer's WebSocketReceive event:</p><pre><code>OnWebSocketReceive←{
     h←⊃⍵
     c←⎕JSON 3⊃⍵
     c.Event≡'SJSR':c.result ⎕TPUT c.id
     d←h.Document
     q←d.StopPropagation
     d.StopPropagation←0
     q:0
     c.HTMLRenderer←h
     j←c.TargetIndex
     k←c.CurrentTargetIndex
     te ce←(Elements d)[j k]
     c.Target←te
     c.CurrentTarget←ce
     h.LastTID←h.LastTID HandleRequest&amp;c
     0
 }
</code></pre><p>This function must handle messages that are instigated by events in the browser, like clicking a button, as well as messages responding to synchronous calls from APL (the "SJSR" event). </p><p>We must allow <code>OnWebSocketReceive</code> to fire continuously without waiting for  the event handler in APL (for the click event on a button, say) to complete. Otherwise if the event handler itself sends a synchronous request back to the browser, the system will deadlock, waiting for a response that will never arrive, as <code>OnWebSocketReceive</code> can never fire as it is waiting for the event handler to complete, which is waiting for the response to the synchronous request. Thus we need to thread something, somewhere.  But this introduces a new problem. The moment we thread, we allow all browser events to be processed concurrently, when we only want SJSR events processed concurrently with respect to all other events. All events initiated by the browser  must be processed sequentially and exclusively, unless of course explicitly threaded somewhere in their own handlers.    </p><p>It is possible to thread <code>OnWebSocketReceive</code> itself, but this only makes it significantly  more difficult to queue the events.  Instead we thread at the very end where we call <code>HandleRequest</code> which waits for the previous request to complete before executing:</p><pre><code>HandleRequest←{
     _←{6::0 ⋄ ⎕TSYNC ⍵}⍺
     _←(⍎⍵.CurrentTarget⍎'On',⍵.Event)⍵
     ~EventToken∊⎕TREQ ⎕TNUMS:0
     ⍵ ⎕TPUT EventToken
 }
</code></pre><p>Because <code>OnWebSockReceive</code> is single-threaded, we are guaranteed that <code>LastTID</code> is updated before the next message is received, and events processed in the proper order.</p><p>If the event is "SJSR", that is if the event is the result of a synchronous JavaScript request, we put a token whose value is the result into the pool, signaling to the waiting thread that it now has its requested result.    The second case for synchronous behavior is testing. We want to be in APL and  initiate an event in the HTMLRenderer that in turn sends a message to APL that does some processing. When this processing is complete, we want to verify some state change.  Let's look at a sample test function: </p><pre><code>TestDecrement←{
     d←⍵.Document
     r←d A.ElementById'result'
     v←⍎⊃r.Content
     b←d A.ElementById'decrement'
     _←b A.FireEventAndWait'click'
     (v-1)≠⍎⊃r.Content
 }
</code></pre><p>This function first inspects and saves the content of an element,  then fires a click event in the HTMLRenderer and waits for the event handler to complete, and finally compares the new value in the element to the expected value. The <code>FireEventAndWait</code> function is:   </p><pre><code>FireEventAndWait←{
     ⍝ ⍺ ←→ Element
     ⍝ ⍵ ←→ Event
     ⍺=0:0
     _←⍺ ExecuteOnElement ⍵,'()'
     ⎕TGET EventToken
 }
</code></pre><p>The <code>EventToken</code> is just a constant. Test functions MUST run in their own thread, separate from the thread the HTMLRenderer is using to process WebSocketReceive events, otherwise no events are processed as we sit waiting for <code>EventToken</code>.    What now happens as we are waiting to get <code>EventToken</code>? If we review the last  couple of lines of <code>HandleRequest</code> above we see that after an event is processed if there is any thread waiting on an EventToken, then it is supplied: </p><pre><code>     ~EventToken∊⎕TREQ ⎕TNUMS:0
     ⍵ ⎕TPUT EventToken 
</code></pre><p>The test framework can thread itself, so all of the tests run in a new thread:   </p><pre><code>RunTests←{
     ⍝ ⍺ ←→ [Namespace of tests]
     ⍝ ⍵ ←→ HTMLRenderer
     ⍺←⊃⎕RSI
     ⎕TID=0:⍺ ∇&amp;⍵
     s h←⍺ ⍵
     c←'Passed' 'Failed' 'Broken' 'N/A' 'Disabled'
     r←{
         b←{0::2 ⋄ (s⍎⍵)h}⍵
         b⊣⎕←⍵,': ',⍕b⊃c
     }¨'T's.⎕NL ¯3
     n←¯1+{≢⍵}⌸r,⍨⍳≢c
     ⍉↑(c,⊂'Total')(n,+/n)
 }
</code></pre><p>The only reason to thread the event handler in <code>OnWebSocketReceive</code> is to allow for synchronous JavaScript from within the event handler. If synchronous JavaScript is not needed, the <code>&amp;</code> and the <code>⎕TSYNC</code> may just be removed, and all event handlers run in thread 0, for what that is worth. Tests need to be threaded regardless, and as a byproduct this allows synchronous JavaScript to run in tests even when not threading the event handler.</p><p>     </p></article></main><footer><p>Copyright 2024 Paul S. Mansour</p></footer></body></html>
